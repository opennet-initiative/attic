#!/usr/bin/awk -f

function prefix(mask,  i, p, m)
{
	split(mask, m, ".")
	i = 1
	p = 0
	while((4 >= i) && (255 == m[i])) {
		i++
		p+=8
	}
	if (4 >= i) {
		while(0 != m[i]) {
			m[i] = int((m[i] + m[i]) % 256)
			p++
		}
	}
	return p
}

function network(ip, mask,  i, a, m)
{
	split(ip, a, ".")
	split(mask, m, ".")
	for(i = 1; i <= 4; i++) {
		a[i] -= a[i] % (256 - m[i])
	}
	return a[1]"."a[2]"."a[3]"."a[4]
}

function broadcast(ip, mask,  i, a, m)
{
	split(ip, a, ".")
	split(mask, m, ".")
	for(i = 1; i <= 4; i++) {
		a[i] += 255 - m[i] - a[i] % (256 - m[i])
	}
	return a[1]"."a[2]"."a[3]"."a[4]
}

function printdev(dev, devname, wdev, var,  p)
{
	print var"DEV="devname
	print var"ADR="ip[dev]
	print var"MSK="mask[dev]
	print var"PRE="prefix(mask[dev])
	print var"NET="network(ip[dev], mask[dev])
	print var"BRC="broadcast(ip[dev], mask[dev])
	print var"MTU="mtu[dev]
	if ("" != wdev && "" != ip[wdev] && "" != mask[wdev] && "" != ip[dev]) {
		print var"OLSR="(network(ip[wdev], mask[wdev]) == network(ip[dev], mask[wdev])?1:"")
	}
}

BEGIN {
	dev=""
	o = FS
	FS = "[: ]+"
	while(getline<"/proc/net/dev") {
		if (/:/) ifconfig[$2] = 1
	}
	while("ifconfig"|getline) {
		if (match($0, "^([^ ]+)")) {
			dev = substr($0, RSTART, RLENGTH)
		}
		else if (/addr:/) {
			ip[dev] = $4
			if ("" != $8) {
				mask[dev] = $8
			}
			else {
				mask[dev] = $6
			}
		}
		else if (/MTU:/) {
			for(i = 1; i < NF; i++) {
				if ("MTU" == $i) {
					mtu[dev] = $(i + 1)
					break
				}
			}
		}
	}
	FS = o

	wdev = ENVIRON["NVRAM_wl0_ifname"]
	if ("" == wdev) "nvram get wl0_ifname"|getline wdev
	printdev(wdev, wdev, "", "WIFI")

	printdev("lo", "lo", "", "LO")

	ldev = ENVIRON["NVRAM_lan_ifname"]
	if ("" == ldev) "nvram get lan_ifname"|getline ldev
	ldevs = ENVIRON["NVRAM_lan_ifnames"]
	if ("" == ldevs) "nvram get lan_ifnames"|getline ldevs
	if ("" == ldev) ldev = "br0"
	if ("" == ifconfig[ldev]) {
		ldev = ldevs
		if (match(ldev, "^([^ ]+)")) {
			ldev = substr(ldev, RSTART, RLENGTH)
		}
		else {
			ldev = "vlan0"
		}
	}
	printdev(ldev, ""!=ip[ldev":0"]?ldev":0":ldev, wdev, "LAN")

	dev = ENVIRON["NVRAM_wan_ifname"]
	if ("" == dev) "nvram get wan_ifname"|getline dev
	if (dev == ldev) dev = ""
	if (dev == wdev) dev = ""
	if (ldevs ~ dev) dev = ""
	if ("" == dev) dev = "vlan1"
	if ("" == ifconfig[dev]) dev = ""
	printdev(dev, dev, wdev, "WAN")

}
